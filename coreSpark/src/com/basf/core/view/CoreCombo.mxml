<?xml version="1.0" encoding="utf-8"?>
<mx:ComboBox xmlns:mx="http://www.adobe.com/2006/mxml" 
			 creationComplete="initControl()"
			 >
	
	<mx:Metadata>
		[Event("dataRetrieved")]
		[Event("dataError")]
	</mx:Metadata>	
	
	<mx:Script>
		<![CDATA[
			import mx.rpc.events.FaultEvent;
			import mx.collections.ArrayCollection;
			import com.basf.core.vo.ComboVO;
			import mx.utils.ObjectProxy;
			import mx.utils.ObjectUtil;
			import com.basf.core.business.ComponentDAO;
			import mx.binding.utils.BindingUtils;
			import com.basf.core.vo.ServiceParameter;
			
			private var dao:ComponentDAO;
			public var serviceName:String;
			public var serviceType:String;
			public var serviceMethod:String;
			public var loadOnCreationComplete:Boolean = true;
			public var serviceParameter:ServiceParameter;
			public var dataProviderNode:String;
			[Bindable]
			public var serviceResult:*;
			[Bindable]
			public var serviceRunning:Boolean = false;
			public var firstDataItem:Object;
			public var defaultSelectedData:Object;
			public var comboLoaded:Boolean = false;
			
			
			private function initControl():void
			{
				dao = new ComponentDAO(serviceName,serviceType,this,dataProviderNode);
				dao.addEventListener("dataRetrieved",dataRetrieved);
				dao.addEventListener(FaultEvent.FAULT,dataError);
				BindingUtils.bindProperty(this,"dataProvider",dao,"dataProvider");
				
				if(loadOnCreationComplete)
					loadData(serviceParameter);				
			}
			
			public function loadData(param:ServiceParameter=null):void
			{
				comboLoaded = false;
				serviceRunning = true;
				errorString = "";
				dao.loadData(param,serviceMethod);
			}
			
			public function dataRetrieved(event:Event):void
			{
				this.serviceResult = dao.serviceResult;
				if (null != firstDataItem)
				{
					ArrayCollection(dataProvider).addItemAt(firstDataItem,0);
					if (null == defaultSelectedData)
						selectedIndex = 0;
					else 
						selectedItem = defaultSelectedData;
				}
				errorString = "";
				comboLoaded = true;
				serviceRunning = false;
				dispatchEvent(new Event("dataRetrieved"));
			}

			public function dataError(event:FaultEvent):void
			{
				errorString = event.fault.message;
				serviceRunning = false;
				dispatchEvent(new Event("dataError"));
			}
			
			
		]]>
	</mx:Script>

</mx:ComboBox>
